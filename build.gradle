plugins {
    id 'application'
    id 'java'
    id 'org.liquibase.gradle' version '2.0.1'
}

group 'ua.univer'

repositories {
        mavenCentral()
        maven() {
            url "http://mvnrepository.com/artifact/"
        }
    }

sourceCompatibility = 1.8
mainClassName = 'ua.univer.rmi.App'


dependencies {
    testImplementation 'junit:junit:4.12'
    implementation 'org.apache.logging.log4j:log4j-api:2.11.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.11.1'
    implementation 'org.apache.commons:commons-dbcp2:2.5.0'
    implementation 'mysql:mysql-connector-java:8.0.14'
    implementation 'org.liquibase:liquibase-core:3.5.5'
    
    //liquibase dependencies
    liquibaseRuntime 'org.liquibase:liquibase-core:3.5.5'
    liquibaseRuntime 'mysql:mysql-connector-java:8.0.14'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.0.1'
}

//load properties
def props = new Properties()
project.file('src/main/resources/config.txt').withInputStream { 
  stream -> props.load(stream) 
}

for(p in props) {
    project.ext[p.key] = p.value
}

def dbUrl = 'jdbc:' + project.Database + '://' + project.FullAddress + '/'  + project.DBName   +  '?'   + project.OptionalParameters

liquibase {        
    activities {
        main {                                         
            changeLogFile 'src/main/resources/db/changelog-master.xml'
            driver project.DriverClass
            url dbUrl
            username project.Login 
            password project.Passw
            logLevel 'info'
        }
    }
}

task customBuild {
    group = 'build'
    
    dependsOn 'clean'
    dependsOn 'update'
    dependsOn 'build'
    tasks.findByName('update').mustRunAfter 'clean'
    tasks.findByName('build').mustRunAfter 'update'
}


test {
    testLogging {
        events = ['standard_out', 'standard_error']
    }
}